(*#-#-#-#-#-#-#-#-#-#---Declaration---#-#-#-#-#-#-#-#-#-#-#-#-#*)
// Matches the given compiled pattern against the subject.
{attribute 'qualified_only'}
FUNCTION FuMatchExpression : BOOL
VAR_INPUT
    typPattern: typCompiledPattern;     // The compiled pattern from "FuCompilePattern"
    psSubject: POINTER TO STRING(1);    // A pointer to the subject string - for technical reasons this is a STRING(1), although any size is supported
    udiLength: UDINT;                   // The length of the subject string
END_VAR
{library PRIVATE}
VAR
    tempPtrStr: POINTER TO BYTE;
    tempPtrStrMatching: POINTER TO BYTE;
    tempStrIndex: UDINT;
    tempStrIndexMatching: UDINT;
    
    i: UINT;
    j: UINT;
    k: LINT;
    
    tempLaziness: BOOL;
    tempHasEndChar: BOOL;
    tempEndChar: BYTE;
    tempEndChar2: BYTE;
    
    tempWildcardSearchStart: LINT;
    tempWildcardSearchEnd: LINT;
    tempWildcardSearchBy: LINT;
    tempWildcardSearchSuccess: BOOL;
END_VAR
(*#-#-#-#-#-#-#-#-#-#---Implementation---#-#-#-#-#-#-#-#-#-#-#-#-#*)
FuMatchExpression := FALSE;

IF typPattern.uiCountTokens = 0 OR_ELSE udiLength = 0 THEN
    RETURN;
END_IF

tempPtrStr := psSubject;
tempStrIndex := 0;

WHILE i < typPattern.uiCountTokens DO
    IF tempStrIndex >= udiLength THEN
        // Only if all following tokens are of type WILDCARD, we can allow a match to be positive
        FOR j := i TO (typPattern.uiCountTokens - 1) BY 1 DO
            IF typPattern.atTokens[j].eType <> eTokenType.WILDCARD THEN
                FuMatchExpression := FALSE;
                RETURN;
            END_IF
        END_FOR
        
        FuMatchExpression := TRUE;
        RETURN;
    END_IF
    
    CASE typPattern.atTokens[i].eType OF
        eTokenType.INVALID: // We shouldn't ever get an invalid token inside our token array that isn't behind our count
            RETURN;
        
        eTokenType.LITERAL:
            IF
                tempPtrStr[tempStrIndex] <> typPattern.atTokens[i].bChar AND_THEN
                (
                    NOT typPattern.xCaseInsensitive OR_ELSE (
                        typPattern.atTokens[i].bAlternativeChar <> 0 AND_THEN
                        tempPtrStr[tempStrIndex] <> typPattern.atTokens[i].bAlternativeChar
                    )
                )
            THEN
                RETURN;
            END_IF
            
            tempStrIndex := tempStrIndex + 1;
        
        eTokenType.ANYCHAR:
            tempStrIndex := tempStrIndex + 1;
        
        eTokenType.WILDCARD:
            tempLaziness := (typPattern.atTokens[i].bChar = 1);
            tempHasEndChar := FALSE;
            
            FOR j := i + 1 TO (typPattern.uiCountTokens - 1) BY 1 DO
                IF typPattern.atTokens[j].eType = eTokenType.LITERAL THEN
                    tempHasEndChar := TRUE;
                    tempEndChar := typPattern.atTokens[j].bChar;
                    tempEndChar2 := typPattern.atTokens[j].bAlternativeChar;
                    EXIT;
                END_IF
            END_FOR
            
            // If we don't have any characters to match on at the end of the pattern, we match immediately
            IF NOT tempHasEndChar THEN
                FuMatchExpression := TRUE;
                RETURN;
            END_IF
            
            IF tempLaziness THEN
                // Advance token pointer once more because the next token is the laziness token we already used
                i := i + 1;
                
                // If the wildcard is lazy, search from front to back
                tempWildcardSearchStart := TO_LINT(tempStrIndex);
                tempWildcardSearchEnd := TO_LINT(udiLength);
                tempWildcardSearchBy := 1;
            ELSE
                // If the wildcard is NOT lazy, search from back to front (greedy!)
                tempWildcardSearchStart := TO_LINT(udiLength);
                tempWildcardSearchEnd := TO_LINT(tempStrIndex);
                tempWildcardSearchBy := -1;
            END_IF
            
            tempWildcardSearchSuccess := FALSE;
            FOR k := tempWildcardSearchStart TO tempWildcardSearchEnd BY tempWildcardSearchBy DO
                IF
                    tempPtrStr[k] = tempEndChar OR_ELSE
                    (typPattern.xCaseInsensitive AND_THEN tempEndChar2 <> 0 AND_THEN tempPtrStr[k] = tempEndChar2)
                THEN
                    tempWildcardSearchSuccess := TRUE;
                    tempStrIndex := TO_UDINT(k);
                    EXIT;
                END_IF
            END_FOR
            
            IF NOT tempWildcardSearchSuccess THEN
                RETURN;
            END_IF
    END_CASE
    
    i := i + 1;
END_WHILE

FuMatchExpression := (tempStrIndex >= udiLength);
